\CD "calloc" 0 D=C;
\OD "open" 0 D=O;
\RD "read" 0 D=R;
\KD "lseek" 0 D=K;

&&-=z

(256 z C) =i;    "iset";
(256 z C) =v;    "val";
((266 z C) (256 z *) +) =s;  "stack";
((266 z C) (256 z *) +) =t;  "return stack";
s=x

"argc";
=k

"argv";
=g

k--;
gz+=g

g sz+ =^
k s =^

0 g^ O =d ;
2 0 d K =q ;;
0 0 d K;;;

q1+ 1 C =h;
q h d R;;;

{] z*+^}
{' r++;r$}
{I z*i+=^}

"inop";
0=j (j 256 <? "{}" j I j++; @)

"ivar";
"{
  sz-=s
  v[p$]s=^
}"=d
'a=j (j'z <=? dj I j++; @)

"idigit";
"{ 
    0=j
    (
     ((j 10 *) (p$ '0 -) +) =j
        p1+$ '0 >=?
        p1+$ '9 <=?
        p++;
    @)
    sz-=s
    js=^
}"=d
'0=j (j'9 <=? dj I j++; @)

"istring";
"{ 
    sz-=s
    p++;
    ps=^
    (p$ 0 ==? } : p$ 34 !=? p++; @)
    0p=$
}" '" I

"ipop";
"{ sz+=s }" '; I

"ieq";
"{
    p++;
    (p$ '= ==?
        s^ sz+=s; s^ == s=^
    :(p$ '$ ==?
        sz+$ s^ =$ sz+z+=s
    :(p$ '^ ==?
        sz+^ s^ =^ sz+z+=s
    :(p$ '& ==?
        s^=s
    :
        s^ vp$z*+ =^ sz+=s
    ))))
}" '= I

"inot";
"{
    s^
    (p1+$ '= ==? sz+=s s^ != p++; : ! )
    s=^
}" '! I

"igt";
"{
    s^ sz+=s s^
    (p1+$ '= ==? <= p++; : < )
    s=^
}" '> I

"ilt";
"{
    s^ sz+=s s^
    (p1+$ '= ==? >= p++; : > )
    s=^
}" '< I

"iadd";
"{
    (p1+$ '+ ==?
        vp1-$z*+=j
        j^1+j=^
        p++;
    :
        s^ sz+=s s^ + s=^
    )
}" '+ I

"isub";
"{
    (p1+$ '- ==?
        vp1-$z*+=j
        j^1-j=^
        p++;
    :        
        s[1] s^ - sz+=s s=^
    )
}" '- I

"imul";
"{ s^ sz+=s s^ * s=^ }" '* I

"idiv";
"{ s[1] s^ / sz+=s s=^ }" '/ I

"imod";
"{ s[1] s^ % sz+=s s=^ }" '% I

"ifwd";
"{
    0=j
    ( p++; p$ 40 ==?
        j++;
    :(p$ 58 ==?
        (j 0 ==? } )
    :(p$ 41 ==?
        (j 0 ==? } )
        j--;
    ))@)
}" ': I

"iback";
"{
    0=j
    ( p--; p$ 41 ==?
        j++;
    :(p$ 40 ==?
        (j 0 ==? } )
        j--;
    )@)
}" '@ I

"icond";
"{
    s^ sz+=s ? }
    : i[':]=f f
}" '? I

"icall";
"{
    tz-=t
    v['r]t=^
    p v'rz*+ =^
    v[p$] =p
}" =c

"iptr";
"{
    s^^ s =^
}" '^ I

"icptr";
"{
    s^$ s =^
}" '$ I

"iaddr";
"{
    s sz-=s s=^
}" '& I

"ifun";
"{
    p++;
    c ip$z*+ =^
    p vp$z*+ =^
    (p$ ?
      (p$ '} ==? } )
    p++ @) 
}" '{ I

"iret";
"{
    v['r]=p
    t^ v'rz*+ =^
    tz+=t
}" '} I

"iremap";
"{
    i[p[2]] ip[1]z*+ =^
    v[p[2]] vp[1]z*+ =^
    p2+=p    
    
}" '\ I

"isys";
"{
   \FD
   v[p$]=F
   s[7] s[6] s[5] s[4] s[3] s[2] s[1] s^ F s =^
}" &^ 'P I
   &^ 'D I
   &^ 'E I
   0 I

"printf" 0 D v'Pz*+ =^;
"dlsym"  0 D v'Dz*+ =^;
"exit"   0 D v'Ez*+ =^;
v['E] v =^;


{L
    10 
    s[3] s[2] s[1] s^ 
    ((s z /) (x z /) -) 
    p$ 
    "----[%c] stack %2ld; [%p, %p, %p, %p]%c"
    P;;;;;;;;
}


{f}
h=p 
(p$ ?
    i[p$]=f 

    L
    f

    p++; 
@)

E
